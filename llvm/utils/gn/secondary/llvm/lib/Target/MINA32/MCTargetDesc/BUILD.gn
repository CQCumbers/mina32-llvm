import("//llvm/utils/TableGen/tablegen.gni")

tablegen("MINA32GenAsmWriter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../MINA32.td"
}

tablegen("MINA32GenInstrInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-instr-info" ]
  td_file = "../MINA32.td"
}

tablegen("MINA32GenMCCodeEmitter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-emitter" ]
  td_file = "../MINA32.td"
}

tablegen("MINA32GenRegisterInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-register-info" ]
  td_file = "../MINA32.td"
}

tablegen("MINA32GenSubtargetInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-subtarget" ]
  td_file = "../MINA32.td"
}

static_library("MCTargetDesc") {
  output_name = "LLVMMINA32Desc"

  # This should contain tablegen targets generating .inc files included
  # by other targets. .inc files only used by .cpp files in this directory
  # should be in deps instead.
  public_deps = [
    ":MINA32GenInstrInfo",
    ":MINA32GenRegisterInfo",
    ":MINA32GenSubtargetInfo",
  ]
  deps = [
    ":MINA32GenAsmWriter",
    ":MINA32GenMCCodeEmitter",
    "//llvm/lib/MC",
    "//llvm/lib/Support",
    "//llvm/lib/Target/MINA32/TargetInfo",
  ]
  include_dirs = [ ".." ]
  sources = [
    "MINA32AsmBackend.cpp",
    "MINA32ELFObjectWriter.cpp",
    "MINA32InstPrinter.cpp",
    "MINA32MCAsmInfo.cpp",
    "MINA32MCCodeEmitter.cpp",
    "MINA32MCExpr.cpp",
    "MINA32MCTargetDesc.cpp",
  ]
}
